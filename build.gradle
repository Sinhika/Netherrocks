plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.163'
}

def getVersionAppendage() {
    if (System.env.BUILD_NUMBER)
        return System.env.BUILD_NUMBER
    else
        return "0"
}

group = mod_group_id
version = "${minecraft_version}-${mod_version}." + getVersionAppendage()

repositories {
    mavenLocal()
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}

// Default run configurations.
// These can be tweaked, removed, or duplicated as needed.
runs {
    // applies to all the run configs below
    configureEach {
        workingDirectory project.file('run')

        // Recommended logging data for a userdev environment
        // The markers can be added/remove as needed separated by commas.
        // "SCAN": For mods scan.
        // "REGISTRIES": For firing of registry events.
        // "REGISTRYDUMP": For getting the contents of all registries.
        systemProperty 'forge.logging.markers', 'REGISTRIES'

        // Recommended logging level for the console
        // You can set various levels here.
        // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    // This run config launches GameTestServer and runs all registered gametests, then exits.
    // By default, the server will crash when no gametests are provided.
    // The gametest system is also enabled by default for other run configs under the /test command.
    gameTestServer {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        workingDirectory project.file('run')
        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        getArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(),
                '--existing', file('src/main/resources/').getAbsolutePath(),'--existing-mod', 'simplecorelib'
    }
} // end runs

// uncomment if including JEI support
sourceSets.main.java { srcDir 'src/api/java' }

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // Specify the version of Minecraft to use.
    implementation "net.neoforged:neoforge:${neo_version}"

    implementation "simplecore-api:SimpleCoreLib:${simplecorelib_version}"
    // implementation "com.github.cech12.BucketLib:neoforge:${current_bucketlib_version}"

    // UNCOMMENT JEI stuff if JEI support ready.
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    compileOnly "mezz.jei:jei-${version_mcjei}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${version_mcjei}-neoforge-api:${jei_version}"
    // compile against the JEI API but do not include it at runtime
    localRuntime "mezz.jei:jei-${version_mcjei}-neoforge:${jei_version}"

    // TOP
    // localRuntime "mcjty.theoneprobe:theoneprobe:${top_version}"
} // end-dependencies


repositories {
    maven {
        name = "mcmodsrepo"
        url = "file:///${project.projectDir}/../mcmodsrepo"
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven { 
        name = "BucketLib"
        url = 'https://jitpack.io' 
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}


// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version: minecraft_version, 
            minecraft_version_range: minecraft_version_range,
            neo_version: neo_version,
            neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, 
            mod_name: mod_name, 
            mod_license: mod_license, 
            version: version,
            mod_authors: mod_authors, 
            simplecorelib_version : simplecorelib_version,            
            simplecore_range: simplecore_range,
            jei_version : jei_version,
            jei_version_range : jei_version_range,
//            current_bucketlib_version : current_bucketlib_version,
//            buckletlib_range : buckletlib_range            
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}


// Example configuration to allow publishing using the maven-publish task

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            groupId = 'netherrocks'
            from components.java
        }
    }
    repositories {
        maven {
            name = "mcmodsrepo"
            url = "file:///${project.projectDir}/../mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
